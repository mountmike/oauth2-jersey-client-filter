plugins {
    id 'java'
    id 'maven-publish'
}

java {
    disableAutoTargetJvm()
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // JUnit 5 dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.11.2'

    // Additional test dependencies
    testImplementation 'org.mockito:mockito-core:5.14.1'
    testImplementation 'org.threeten:threeten-extra:1.8.0'
    testImplementation 'org.slf4j:slf4j-simple:2.0.16'

    // Other dependencies
    implementation 'org.slf4j:slf4j-api:2.0.16'
    implementation 'org.glassfish.jersey.core:jersey-client:3.1.9'
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:3.1.9'
    implementation 'org.glassfish.jersey.security:oauth2-client:3.1.9'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:3.1.9'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
    implementation 'io.vertx:vertx-auth-oauth2:4.5.10'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/AllTheDucks/oauth2-jersey-client-filter")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
            }
        }
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile).tap {
            configureEach {
                options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
            }
        }
    }
}